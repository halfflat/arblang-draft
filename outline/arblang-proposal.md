# Arblang proposal (WIP)

Arblang is a language for defining the dynamics and effects of Arbor 'mechanisms'. A mechanism is a stateful process whose evolution over time is governed by a system of ODEs and by responses to external events, and which in turn acts upon the cellular state to which the mechanism is applied.

The arblang language is constrained so that every evolution and effect can be interpreted _symbolically_ as closed form expressions. This is to permit the automatic analysis of these expressions for the purposes of determining a priori analytic solutions or appropriate ODE approximation techniques, and to automatically determine, for example, time derivatives of effect values through symbolic or automatic differentiation. Consequently common programming constructs such as unbounded iteration or recursion are precluded.

Arblang nonetheless can also be interpreted in a purely numerical fashion in other contexts.


# Mechanism semantics

A mechanism can be one of a number of different _classes_ of mechanism, where each class determines the sort of cell interactions that the mechanism performs. The classes are pre-defined, but are open ended, in that further extensions to Arbor may admit new mechanism classes. The basic set of classes are described as follows:

* _Density mechanisms_ describe processes with spatial extent, and their effects are described in terms of current densities and molar fluxes of ionic and nonionic species. They do not receive events.

* _Concentration models_ determine the evolution of internal or external spieces concentration over some spatial extent, based upon the species fluxes and other cellular state. They also do not receive events.

* _Point mechanisms_ describe localized processes, with effects defined in terms of currents and molar flow rates. They can change their state in response to spikes from other cells from connections that are bound to them, or to spikes generated by the cell on which they are placed.

Each of these mechanisms can have a _state_ comprising zero or more named values that vary over time. Each mechanism definition will provide the initial values for this state, and define their evolution by an explicit system of ODEs.


## External bindings

The definition of a mechanism allows for names to be bound to the mechanism state and various cellular quantities so that they can play a role in defining the evolution and effects of the mechanism.

The list of possible cellular quantities is open to future extension, but initially are as follows:

* _State_ refers to the evolving state of the mechanism itself.

* _Membrane potential_ is the potential difference across the cellular membrane.

* _Temperature_ is the local temperature at the cellular membrane.

* _Molar flux_ is the local transmembrane flux associated with an ionic or nonionic species.

* _Current density_ is the local transmembrane current associated with an ionic species. This is related to the species molar flux by the ionic charge and the Faraday constant.

* _Charge_ is the fixed electrical charge of an ionic species, given as a dimensionless multiple of the the elementary charge.

* _Internal concentration_ and _external concentration_ are the intracellular and extracellular concentrations respectively of a species, in the vicintiy of the cellular membrane.

Not every mechanism class has access to every cellular quantity. In particular, only concentration models have access to current densities and molar fluxes.


## Effects

Effects define how the mechanism state influences the cellular state. The list of possible effects is open to future extension, but the initial effects are as follows:

* _Current_ defines an ionic current contribution or non-specific current contribution (one not explicitly associated with any ionic species) to the transmembrane currents by a localized process.

* _Current density_ defines similarly a contribution to the ionic or non-specific transmembrane current densities by a process with spatial extent.

* _Molar flow rate_ defines a contribution to the transmembrane flow of a species by a localized process.

* _Molar flux_ defines a contribution the transmembrane flux of a species by a process with spatial extent.

* _Internal concentration rate_ and _External concentration rate_ define the contribution to the rate of change in the intracellular or extracellular concentrations respectively of a given species.

A mechanism can define an effect in terms of an ionic current (or current density) or in terms of a species flow (or flux), but not both. A current defines an ionic flow, and vice versa.

Restrictions:

* Concentration models can only have concentratione effects.

* Point mechanisms can only have current or molar flow rate effects.

* Density mechanisms can only have current density or molar flux effects.


## Events

As noted above, point mechanisms can receive _events_, upon which their state can be modified according to some formula. There are currently two supported sorts of events:

* _Event_ is triggered when by a spike on an attached connection. It has a scalar value equal to the connect weight.

* _Post_ is triggered when a spike is generated on the post-synaptic cell. There may be a delay between the spike generation and the _post_ event; the value of the _post_ event is the time between the spike generation and the event delivery.


## Parameters

A mechanism can include _parameters_. These are named quantities with a default value that are constant throughout the evolution of a mechanism's state, but which can be overriden with user-supplied values when the mechanism is used in a cell model.

For a parameter to be settable, it must be explicitly exported in the mechanism definition.


# Lexical grammar

Tokenization is governed by a grammar, described below in a variant of BNF, with the notation:

* Names for non-terminals are written in _italics_ and may not contain spaces.

* Non-terminals that constitue tokens are written in ***bold-italics***.

* Terminals are written in `monospace`, denoting the exact character sequence thus represented.

* A terminal may also be written as a unicode code point, as 'U+' followed by four or five hexadecimal digits followed optionally by the literal character and then optionally by the unicode name in all capitals.

* A set of single character terminals can be denoted by {expr} where _expr_ is a one- or two-letter Unicode general category abbreviation, indicating the corresponding collection of characters, or a binary Unicode property, indicating the collection of characters for which the property has the value 'Yes'.

* A set of single character terminals can also be denoted by a character class […] following the syntax described in [Unicode Regular Expressions](https://www.unicode.org/reports/tr18) sections 0.1.1, 1.2.4, and 1.3.

* Symbols can be grouped with parentheses.

* Alternatives are delimitted by a vertical bar |.

* An asterisk indicates the preceding symbol or group is repeated zero or more times.

* A plus sign indicates the preceding symbol or group is repeated one or more times.

* A question mark indicates the preceding symbol or group may be omitted, that is, it can appear at most one time.


## Character set

Arblang source is represented as a sequence of Unicode characters in normalization form C (NFC), excluding unassigned characters, surrogate code points, noncharacters, and byte order marks. It is the responsibility of the interpreting environment to transform any particular source encoding to this representation.


## Tokenization

A lexically valid Arblang source must admit a tokenization as a sequence of ***comment*** tokens, ***whitespace*** tokens, ***symbol*** tokens, ***string-literal*** tokens, ***superscript-literal*** tokens, ***numeric-literal*** tokens, and punctuation tokens.

A token has an associated value, which may be empty. If not empty, it is the result of applying a token-specific canonicalization process.
With the exception of string literals, canonicalization first applies Unicode compatibility normalization NFKC to the token text, then further processing as detailed below.


### Comments

A comment is introduced by `#` and then extends to the end of the line. It is the only context in which a line end is not regarded simply as whitespace.

> ***comment*** ::= U+0023 `#` NUMBER SIGN _comment-character_* _new-line_
>
> _comment_character_ ::= [^\\u{000A}\\u{000B}\\u{000C}\\u{000D}\\u{0085}\\u{2028}\\u{2029}]
>
> _new-line_ ::= _cr_ | _lf_ | _cr_ _lf_ | _other-newline_
>
> _lf_ ::= U+000A LINE FEED
>
> _cr_ ::= U+000D CARRIAGE RETURN
>
> _other-new-line_ ::= U+0085 NEXT LINE | U+2028 LINE SEPARATOR | U+2029 PARAGRAPH SEPARATOR

#### Token value

A ***comment*** token has no value.

#### Notes

A _comment-character_ is any character that is not the first character of _new-line_. New line characters and sequences correspond to the [line boundaries requirement RL1.6 of Unicode Regular Expressions](https://www.unicode.org/reports/tr18/#RL1.6).


### Whitespace

In some expressions, where it denotes multiplication, whitepace is mandatory, but otherwise whitespace is not significant. It comprises any non-empty sequence of characters with the White_Space property.

> ***whitespace*** ::= {White_Space}\+

#### Token value

A ***whitespace*** token has no value.


### Symbols

Symbols correspond to Arblang identifiers and keywords. Arblang keywords are generally contextual: the interpretation of a symbol as an identifier or a keyword is determined in parsing.

> ***symbol*** ::= _symbol-start_ _symbol-continue_\* | _symbol-degree-prefixed_
>
> _symbol-start_ ::= {L} | {Nl} | {Other_ID_Start}
>
> _symbol-continue_ ::= _symbol-start_ | {Nd} | {Mn} | {Mc} | {Pc} | _prime-mark_
>
> _prime-mark_ ::= U+0027 `'` APOSTROPHE | U+02B9 `ʹ` MODIFIER LETTER PRIME | U+2032 `′` PRIME
>
> _symbol-degree-prefixed_ ::= U+00B0 `°` DEGREE SIGN {L}+ | U+2109 `℉` DEGREE FARENHEIT | U+2109 `℃` DEGREE CENTIGRADE

#### Token value

Canonicalization substitutes any character in _prime-mark_ with U+0027 APOSTROPHE after performing NFKC normalization.

#### Notes

Identifiers broadly follow the conventions of Python, except:
* `_` is not a valid _symbol-start_.
* A ***symbol*** may contain _prime-mark_ characters.
* Special degree-sign prefixed symbols are permitted, in order to represent temperature scales. Compatability characters U+2109 `℉` and U+2109 `℃` will be converted to `°F` and `°C` respectively in NFKC normalization.
* Characters that are only _compatibility_ equivalent to an acceptable _symbol_ character are not permitted. In particular, superscript numerals are not permitted within a _symbol_.


### Superscript literals

A superscript literal is used to denote exponents in the absence of the exponentiation operator.

> ***superscript-literal*** ::= _superscript-minus_? _superscript-digit_+
>
> _superscript-minus_ ::= U+207B `⁻` SUPERSCRIPT MINUS
>
> _superscript-digit_ ::= `⁰` | `¹` | `²` | `³` | `⁴` | `⁵` | `⁶` | `⁷` | `⁸` | `⁹`

#### Token value

After NFKC normalization, which takes superscript digits and minus to regular digits and U+2212 MINUS SIGN, the token value is canonicalized by mapping U+2212 to U+002d `-` HYPHEN-MINUS.


### Numeric literals

Numeric literals represent both fractional and integer literal values.

> ***numeric-literal*** ::= _digit-sequence_ (_decimal-separator_ _digit-sequence_)? _exponent_?
>
> _digit-sequence_ ::= ( `0` | `1` | `2` | `3` | `4` | `5` | `6` | `7` | `8` | `9` )+ ( _digit-group-separator_ _digit_sequence_)?
>
> _digit-group-separator_ ::= U+0027 `'` APOSTROPHE | _whitespace_
>
> _decimal-separator_ ::= U+002E `.` FULL STOP
>
> _exponent_ ::= _whitespace_? _exponent-E_ | _exponent-alternate_
>
> _exponent-E_ ::= ( 'E' | 'e' ) _sign_? _digit-sequence_
>
> _exponent-alternate_ ::= U+00D7 `×` MULTIPLICATION SIGN _whitespace_? `10` _superscript-literal_
>
> _sign_ ::= U+002B `+` PLUS SIGN | U+002D `-` HYPHEN-MINUS | U+2212 `−`MINUS SIGN

#### Token value

After normalization, which takes superscript digits and minus to regular digits and U+2212 MINUS SIGN, the token value is canonicalized as follows.
* Any _digit-group-separator_ is removed (including all whitespace).
* U+2212 MINUS SIGN is mapped to U+002D `-` HYPHEN-MINUS.
* U+002B PLUS SIGN is removed.
* Exponential markers `e` and `×10` are replaced with `E`.

#### Notes

* The use of whitespace as a digit group separator is intended to accommodate SI practice of writing long numbers, e.g. `10 000 000` for 10⁶.
* The alternate scientific number syntax `1.234 45 × 10³` is allowed in order to support standard SI practice. `×` is not used elsewhere as an arithmetic operator.
* The apostrophe digit separator is also used in C++14 for integer literals.
* In order to accommodate loss-free rescaling of unit-bearing quantities, real and integer literals in arithmetic contexts should have an internal representation that maintains a normalized signifcand and a power of ten scale.


### String literals

String literals encode arbitrary character sequences used to specify and identify interface and species names.

> ***string-literal*** ::= `"` ( [^&#92;"]* | _escape-sequence_ )\* `"`
>
> _escape-sequence_ ::= `\\` | `\"`

#### Token value

The value is taken as the characters between the initial and final `"` characters, followed by the replacement of each _escape-sequence_: `\\` by `\` and `\"` by `"`.

#### Notes

The value of a ***string-literal*** token does _not_ undergo NFKC normalization, and so comprises an arbitrary sequence of Unicode characters in NFC normalization. While the syntax allows arbitary white space within a literal, including line separators and similar, the interpreting environment might impose further restrictions on what constitutes a valid interface name, for example.


### Punctuation

> _punctuation_ ::=
>     ***plus-sign*** | ***minus-sign*** | ***multiplication-dot*** | ***division-slash*** | ***exponent-op*** | ***preferential-union*** |
>     ***compare-equal*** | ***compare-not-equal*** | ***compare-less*** | ***compare-less-eqaul*** | ***compare-greater*** | ***compare-greater-eqaul*** |
>     ***left-arrow*** | ***right-arrow*** | ***right-left-arrow*** |
>     ***assign-equal*** | ***semicolon*** | ***left-paren*** | ***right-paren*** | ***left-brace*** | ***right-brace*** |
>     ***colon*** | ***bar*** | ***comma*** | ***period***

Punctuation token definitions:

> ***plus-sign*** ::= `+`

> ***minus-sign*** ::= U+002D `-` HYPHEN-MINUS | U+2212 `−`MINUS SIGN

> ***multiplication-dot*** ::= U+00B7 `·` MIDDLE DOT | U+22C5 `⋅` DOT OPERATOR

> ***asterisk*** ::= `*`

> ***division-slash*** ::= U+002F `/` SOLIDUS | U+2215 `∕` DIVISION SLASH

> ***exponent-op*** ::= `^`

> ***preferential-union*** ::= `&` | U+2294 `⊔` SQUARE CAP

> ***compare-equal*** ::= `==`

> ***compare-not-equal*** ::= `!=` | U+2260 `≠` NOT EQUAL TO

> ***compare-less*** ::= `<`

> ***compare-less-equal*** ::= `<=` | U+2264 `≤` LESS-THAN OR EQUAL TO

> ***compare-greater*** ::= `>`

> ***compare-greater-eqaul*** ::= `>=` | U+2265 `≥` GREATER-THAN OR EQUAL TO

> ***left-arrow*** ::= `<-` | U+2190 `←` LEFTWARDS ARROW

> ***right-arrow*** ::= `->` | U+2192 `→` RIGHTWARDS ARROW

> ***right-left-arrow*** ::= `<->` | U+21C4 `⇄` RIGHT ARROW OVER LEFT ARROW

> ***assign-equal*** ::= `=`

> ***semicolon*** ::= `;`

> ***left-paren*** ::= `(`

> ***right-paren*** ::= `)`

> ***left-brace*** ::= `{`

> ***right-brace*** ::= `}`

> ***colon*** ::= `:`

> ***bar*** ::= U+007C `|` VERTICAL LINE

> ***comma*** ::= `,`

> ***period*** ::= `.`

#### Token value

Punctuation tokens have no value.


# Arblang syntax and semantics

The syntax definitions below are defined in terms of the tokens defined in the lexical grammar, with the following conventions:

* A ***symbol*** token can represent an identifier or a keyword; keywords in the syntax descriptions are written in `monospace`, and should be interpreted as ***symbol*** tokens with the corresponding value.

* Where ***whitespace*** is present in a syntax rule, it is required, but additional ***whitespace*** tokens are permitted anywhere in the (tokenized) arblang source, where they are ignored.

* For readability in syntax rules, the text representation of a punctuation token, styled monospace, may stand in for the punctuation token itself. So, for example, `=` might stand for the token ***assign-equal***.

An arblang source document comprises a series of module and interface definitions.

> _source_ ::= ( _module-defn_ | _interface-defn_ )+

Module and interface definitions are described below.

## Identifiers and scope

Identifiers are represented by ***symbol*** tokens and depending on context, may refer to:
1. a module,
2. an imported module,
3. a constant,
4. a parameter,
5. a function,
6. an external binding,
7. a type alias,
8. a regime,
9. a record field,
10. a function argument,
11. or a local value binding.

### Scopes

The association of an identifier with its referrent is called a _binding_, and that binding is valid only within a _scope_. With one exception for regime names, an identifer's scope does not precede the point where that identifier is introduced by some declaration or binding.

**Global scope.**
A module definition introduces the module name into the following global scope. The environment may also introduce module names defined in other arblang sources.

**Module scope.**
Within a module or interface definition, top-level parameter, constant, type alias, and function definitions have module scope, as do module imports and external bindings. The scope extends from after the definition or import, until the end of the enclosing module or interface. Identifiers bound in module scope can be made visible in other modules as a _qualified-identifier_ after a module import.

**Function scope.**
Function arguments have function scope, which extends until the end of the function definition.

**Regime scope.**
All top-level regime definitions in an interface have as scope the entire interface definition, even preceding the regime definition. The same applies within a region definition: the scope of an internal region identifier is the entire body of the region definition. An internal region identifier is visible as a _qualified-identifier_ elsewhere within the same interface definition.

**Expression scope.**
`let` and `with` binding expressions introduce new identifiers that have as scope the final expression in the binding.

**Record scope.**
Field names introduced in a record type or record value have record scope, which extends to the end of the record type or record value construction.

Scope form a hierarchy of inner and outer scopes, with the outermost scope being the global scope.

### Context

Two different identifers with the same ***symbol*** may be bound in the same scope, if they are in different _contexts_. There are five distinct contexts in arblang:

1. _Module_ context applies wherever a module name is being introduced in a module definition, or where a module is referenced in a module import clause.
2. _Type context_ applies in type expressions and where a type alias is introduced.
3. _Regime context_ applies in regime clauses and where a regime name is introduced in a regime definition.
4. _Record context_ applies to field names within a record type description or in a record value construction.
5. _Expression context_ applies within value expressions, function argument definitions, the definition of parameters, constants, functions, and in the association of an identifier with an imported module.

Each context can be regarded as constituting a different namespace for identifiers.

### Masking

An identifer bound in a given context in an outer scope may be _masked_ by a binding of an identifier with the same ***symbol*** in an inner scope. This is permitted only in the following circumstances:

1. An identifer bound in expression context in a function scope by a function argument or in expression scope by a `let` or `with` binding may mask an identifer in an outer scope.
2. An identifier bound in regime context in an inner regime definition may mask a regime identifer in an outer regime or interface scope.

It is otherwise an error to bind an identifer with the same ***symbol*** as an already bound identifier in the same context.

### Qualified identifers

A qualified identifer is a term of the form:

> _qualified-identifier_ ::= ***symbol*** ( `.` ***symbol*** )\*

A qualified identifier of the form α._id_, where α is a (qualified) identifier and _id_ is an identifier, is bound to the referent of _id_ in the scope determined by α:

1. In an expression context, if α is bound to a record value, then α._id_ is bound to the value of the field named _id_ in α.
3. In an expression context, if α is bound to an imported module, then α._id_ is bound to the constant, parameter, or function bound to _id_ in the module refered to by α.
2. In a type context, if α is bound to an imported module, then α._id_ is bound to the type bound to _id_ in in the module refered to by α.
3. In a region context, f α is bound to a regime, then α._id_ is bound to the inner regime named _id_ in α.

### Examples

```
# Binds 'foo' in module context, global scope.
module foo {
    # Binds 't' in expression context, module scope.
    def t: time = 10 s;
}

module bar {
    # Binds 'foo' in type context, module scope.
    type foo = length;

    # Binds 'foo' in expression context, module scope.
    def foo: foo = 3 m;

    # Refers to 'foo' in module context, from outer global scope.
    # Binds 'F' in expression context, module scope.
    import foo as F;

    # Binds 'quux' in expression context, module scope.
    def quux =
        # 'let' rebinds 'F' in the subsequent expression context in
        # expression scope.
        #
        # On the right hand side, 'F' is still bound to the module import,
        # and 'u', a record field name, is bound in record context in the
        # scope of the record value construct.
        #
        # 'F.t' is a qualified identifier, that is bound to the constant 't'
        # defined in the module 'foo'.
        let F = { u = F.t; };

        # 'F' refers to the record above; 'F.u' is a qualified identifier
        # that refers to the value bound to the field 'u' in 'F'.
        F.u / 20 s;

    # Error: foo cannot refer to both a value and an imported module in module scope.
    import foo;

    # Error: foo cannot refer to both a value and a function in module scope.
    def foo = fn () → 4;
}
```

## Types and literals

Every expression has a type, which is either:

1. Boolean.
2. A quantity (see below).
3. A record type, comprising an unordered sequence of named fields, with each field being either a quantity or another record type.
4. A function.

A type expression _type-expr_ identifies a particular type, and is used in type assertions and function argument specifiers. Function types have no corresponding type expression: expressions of a function type may only be bound to an identifier or used in a function application expression. In particular, they may not be used as an argument in a function application.

A _type-expr_ is either `boolean`, a quantity term, a record type description, or a (qualified) identifier bound to a type expression by a type alias.

> _type-expr_ ::= `boolean` | _quantity-term_ | _record-type-expr_ | _qualified-identifier_

An expression of a given type can be constructed from type literals and constructors described below, as well as from operators acting on subexpressions (see the section _Expressions_).

In definitions of record fields, function arguments, and optionally within expressions a _type-assertion_ declares that the identifier or expression has a given type.

> _type-assertion_ ::= `:` _type-expr_

If the type of an expression bound to an identifier is neither the same as nor a subtype of the type in the assertion, then the binding is ill-formed. Sub- and supertypes constitute a relation between record types, described below.

Examples of type assertions:

```
# Valid: 3 m has type `length`.
def a: length = 3 m;

# Valid: 3 m has type `length`.
def b = 3 m: length;

# Valid: the type `small` is a supertype of `big` and the
# function `f` has result of type `length`.
type big = { a: real; b: { x: length; y: length; }; c: mass; };
type small = { c: mass; b: { x: length; } };
def f = fn (p: small) → p.b.x + 2 m;
def g = fn (q: big) → f(q) : length;

# Ill-formed: rhs is not of type `length`
def c: length = 3;
```

### Boolean

The boolean type has two possible values, true and false, and expressions of boolean type are constructed from the boolean literals and comparison expressions.

> _boolean-literal_ ::= `true` | `false`

The boolean literals are keywords in an expression context, and may not be used as identifiers in this context.

### Quantities

Quantities represent physical quantities, which in turn comprise a magnitude and a physical dimension. The specific unit scale underlying the representation of a physical quantity is implicit.

A quantity type is defined as a product term of named quantities such as voltage, time, resistance, etc. The set of named quantities is predefined, and cannot be extended within arblang.

> _quantity-term_ ::= _quantity-name_ _integer-exponent_? | _quantity-product_ | _quantity-quotient_
>
> _quantity-product_ ::= _quantity-term_ ( ***whitespace*** | ***multiplication-dot*** )  _quantity-term_
>
> _quantity-quotient_ ::= _quantity-term_ ***division-slash*** _quantity-term_
>
> _integer-exponent_ ::= `^` ***minus-sign***? ***numeric-literal*** |  ***superscript-literal***
>
> _quantity-name_ ::= `real` | `length` | `mass` | `time` | `current` | `temperature` | `amount` |
>   `frequency` | `area` | `volume` | `velocity` | `acceleration` | `momentum` | `force` | `pressure` | `power` | `energy` |
>   `entropy` | `charge` | `voltage` | `capacitance` | `inductance` | `resistance` | `conductance` | `molarity`

The ***numeric-literal*** in an _integer-exponent_ term must be integral — the token value may not contain a _decimal_separator_ or _exponent_. A non-integral exponent is a syntax error.

Here, `real` denotes the dimensionless (scalar) quantity.

The named quantities above are chosen to represent ISQ quantities, but in some cases a single word is used to represent a longer ISQ quantity name to simplify the grammar. The named quantities are listed below with their equivalent ISQ quantity and dimensional expression in terms of the physical dimensions **L** (length), **M** (mass), **T** (time), **I** (electric current), **Θ*** (thermodynaic temperature), and **N** (amount of substance).

| Arblang name   | ISQ name (if different)   | ISO document | Dimension      |
|----------------|---------------------------|--------------|----------------|
| `real`         | quantity of dimension one | 80000-1      | **1**          |
| `length`       |                           | 80000-1      | **L**          |
| `mass`         |                           | 80000-1      | **M**          |
| `time`         |                           | 80000-1      | **T**          |
| `current`      | electric current          | 80000-1      | **I**          |
| `temperature`  | thermodynamic temperature | 80000-1      | **Θ**          |
| `amount`       | amount of substance       | 80000-1      | **N**          |
| `frequency`    |                           | 80000-3      | **T⁻¹**        |
| `area`         |                           | 80000-3      | **L²**         |
| `volume`       |                           | 80000-3      | **L³**         |
| `velocity`     |                           | 80000-3      | **LT⁻¹**       |
| `acceleration` |                           | 80000-3      | **LT⁻²**       |
| `momentum`     |                           | 80000-4      | **LMT⁻¹**      |
| `force`        |                           | 80000-4      | **LMT⁻²**      |
| `pressure`     |                           | 80000-4      | **L⁻¹MT⁻²**    |
| `power`        |                           | 80000-4      | **L²MT⁻³**     |
| `energy`       | kinetic energy            | 80000-4      | **L²MT⁻²**     |
|                | thermodynamic energy      | 80000-5      | **L²MT⁻²**     |
| `entropy`      | entropy                   | 80000-5      | **L²MT⁻²Θ⁻¹**  |
| `charge`       | electric charge           | 80000-6      | **TI**         |
| `voltage`      |                           | 80000-6      | **L²MT⁻³I⁻¹**  |
| `capacitance`  |                           | 80000-6      | **L⁻²M⁻¹T⁴I²** |
| `inductance`   |                           | 80000-6      | **L²MT⁻²I⁻²**  |
| `resistance`   |                           | 80000-6      | **L²MT⁻³I⁻²**  |
| `conductance`  |                           | 80000-6      | **L⁻²M⁻¹T³I²** |
| `molarity`     | amount-of-substance concentration | 80000-9 | **L⁻³N**    |

For arblang, two quantity type expressions are deemed equivalent if they have the same physical dimensionality. Equivalent type expressions describe the same type.

Quantity literals are composed from a ***numeric-literal*** and an optional _unit-term_ suffix. Units are represented by keywords corresponding to combinations of SI unit prefixes and SI unit abbreviations, and similarly to quantities, are composed via multiplication, division, and integer exponentiation. If no _unit-term_ suffix is present, the quantity is a scalar `real` type (i.e. a quantity of dimension one).

> _quantity-literal_ ::= ***minus-sign***? ***numeric-literal*** ( ***whitespace*** _unit-term_ )?
>
> _unit-term_ ::= _unit-name_ _integer-exponent_? | _unit-product_ | _unit-quotient_
>
> _unit-product_ ::= _unit-term_ ( ***whitespace*** | ***multiplication-dot*** )  _unit-term_
>
> _unit-quotient_ ::= _unit-term_ ***division-slash*** _unit-term_
>
> _unit-name_ ::= ***symbol***

A valid unit name is a ***symbol***, but are _lexically_ described as follows.

> _unit-name_ ::= `°C` | _unit-SI-prefix_? _unit-metric_
>
> _unit-SI-prefix_ ::=  `Y` | `Z` | `E` | `P` | `T` | `G` | `M` | `k` | `h` | `da` | `d` | `c` | `m` | `μ` | `u` | `n` | `p` | `f` | `a` | `z` | `y`
>
> _unit-metric_ ::= `m` | `g` | `s` | `A` | `K` | `mol` | `Hz` | `L` | `l` |  `N` | `Pa` | `W` | `J` | `C` | `V` | `F` | `H` | `Ω` | `Ohm` | `S` | `M` | `kat`

Unit prefixes are SI unit prefix symbols, with the addition of `u` for micro-:

| Unit prefix | SI unit name | Multiple |
|-------------|--------------|----------|
| `Y`  | yotta | 10²⁴  |
| `Z`  | zetta | 10²¹  |
| `E`  | exa   | 10¹⁸  |
| `P`  | peta  | 10¹⁵  |
| `T`  | tera  | 10¹²  |
| `G`  | giga  | 10⁹   |
| `M`  | mega  | 10⁶   |
| `k`  | kilo  | 10³   |
| `h`  | hecto | 10²   |
| `da` | deca  | 10¹   |
| `d`  | deci  | 10⁻¹  |
| `c`  | centi | 10⁻²  |
| `m`  | milli | 10⁻³  |
| `μ`  | micro | 10⁻⁶  |
| `u`  | micro | 10⁻⁶  |
| `n`  | nano  | 10⁻⁹  |
| `p`  | pico  | 10⁻¹² |
| `f`  | femto | 10⁻¹⁵ |
| `a`  | atto  | 10⁻¹⁸ |
| `z`  | zepto | 10⁻²¹ |
| `y`  | yocto | 10⁻²⁴ |

Arblang metric units, and their corresponding arblang quantities:

| Unit  | Name    | Arblang quantity |
|-------|---------|------------------|
| `m`   | metre   | `length`         |
| `g`   | gram    | `mass`           |
| `s`   | second  | `time`           |
| `A`   | ampere  | `current`        |
| `K`   | kelvin  | `temperature`    |
| `mol` | mole    | `amount`         |
| `Hz`  | hertz   | `frequency`      |
| `L`   | litre   | `volume`         |
| `l`   | litre   | `volume`         |
| `N`   | newton  | `force`          |
| `Pa`  | pascal  | `pressure`       |
| `W`   | watt    | `power`          |
| `J`   | joule   | `energy`         |
| `C`   | coulomb | `charge`         |
| `V`   | volt    | `voltage`        |
| `F`   | farad   | `capacitance`    |
| `H`   | henry   | `inductance`     |
| `Ω`   | ohm     | `resistance`     |
| `Ohm` | ohm     | `resistance`     |
| `S`   | siemens | `conductance`    |
| `M`   | molar   | `molarity`       |
| `kat` | katal   | `amount/time`    |

Note that the non-SI unit 'molar' is equal to 1 mol/L.

#### Notes

**TODO**: Put this under expressions below, in subsection _Offset value arithmetic_.

There is one unit, degrees Celsius °C, which is not zero-based. Depending on context, a value expressed as _x_ °C may represent an absolute temperature, viz. (_x_+273.15) kelvin, or a temperature difference of _x_ kelvin. To avoid confusion, it is recommended that any arblang source only use °C to represent the former. For the interpretation of arblang, there are three chief possibilities, in order of increasing sophistication:

1. Simplest: always automatically convert _x_ °C to (_x_+273.15) kelvin. Use of a Celsius temperature literal in non-absolute contexts is an unflagged semantic error.
2. Conservative contextual approach: subtraction is always regarded as representing a difference, so that two compatible offset or offset-free values may be subtracted, giving an offset-free value; an offset value may be the left hand summand in an addition with a compatible non-offset value, giving an offset value. All other arithmetical expressions constitute a type error.
3. Fully contextual approach: as above, save that an offset value has its offset ignored in any arithmetical expression _except_ when it is an operand in a subtraction, or is the left hand summand in an addition.

Examples for the different interpretations, writing _x_ ⊕ _y_ for an internal representation of a value with implicit offset _y_:

| Expression      | Simple    | Conservative    | Full contextual |
|-----------------|-----=-----|-----------------|-----------------|
| 1 °C            | 274.15 K  | (1 ⊕ 273.15) K  | (1 ⊕ 273.15) K  |
| 1 °C + 3 K      | 277.15 K  | (4 ⊕ 273.15) K  | (4 ⊕ 273.15) K  |
| 1 °C - 3 °C     | -2 K      |  -2 K           | -2 K            |
| 280.15 K + 3 °C | 283.14 K  |  _error_        | 283.15 K        |
| 280.15 K - 3 °C | -2 K      |  _error_        | -2 K            |
| 1 °C + 3 °C     | 550.30 K  |  _error_        | (4 ⊕ 273.15) K  |
| 1 °C / 2        | 137.075 K |  _error_        | 0.5 K           |

All three approaches can lead to surprsing behaviour, given the differences in interprertation between addition and subtraction with offset-bearing quantities. The best approach might be to remove the °C unit altogether.


### Records

A record is a labelled unordered tuple of values which are either quantities or records themselves. A record may not have two fields of the same name. A record type specification has the syntax:

> _record-type-expr_ ::= `{` _field-spec_* `}`
>
> _field-spac_ ::= ***symbol*** _type-assertion_ `;`
>
> _type-assertion_ ::= `:` _type-expr_

A record type _R_ with fields _fᵢ_ of type _Tᵢ_ is a supertype of a record type _S_ with fields _gⱼ_ of type _Uⱼ_ if for every _i_ there exists a _j_ such that _fᵢ_ and _gⱼ_ have the same identifier and _Tᵢ_ is the same type as or a supertype of _Uⱼ_. If _R_ is a supertype of _S_ and _S_ is a supertype of _R_, then _R_ and _S_ are the same type. Note that the order of fields in a _record-type_ is arbitrary.

Correspondingly, record literals have the syntax:

> _record-literal_ ::= `{` _field-definition_* `}`
>
> _field-definition_ ::= ***symbol*** _type-assertion_? `=` _expression_ `;`

For field definitions, if there is no type assertion given, the type of the field is deduced from the expression on the right hand side.

The field names constituting the left hand side of the field definitions have the _record scope_ of the record value. The defining expressions on the right hand side remain in an expression context, so that identifiers with the same symbol as a record field will _not_ refer to that record. For example, in the expression

```
let a = 3 m;
let r = { a = 4; b = a; };
r.b
```

`r` has type `{ a: real; b: length; }` and is bound to the value `{ a = 4; b = 3 m; }`, because the `a` in the right hand side of `b = a` is in expression context, and so is bound to to the value 3 as given in the outer `let`.

Field values in a record are accessed via a qualified identifier (see above), or by immediate field access (see Record expressions, below), or brought into scope via a `with` binding (see Value bindings, below).

```
let r = { a = 4; }; r.a   # qualified identifer evalates to value 4
let x = { a = 4; }.a; x   # immediate field access evalates to value 4
with { a = 4; }; a        # a is locally bound to the value 4
```

#### Notes

The sub-/supertype relationships could be extended to support quantities with a restricted range, exposed in the syntax by an extension of the _type-assertion_ syntax. For example, the simplest sort of contrainst, that a value be within some fixed interval, could be represented by syntax such as `a: (-10 m, ∞ m) length`.

### Functions

A function literal gives a value of a function type. Function types have no representation in the arblang source language, and so cannot be used in type assertions, or in function arguments.

> _function-literal_ ::= `fn` `(` ( _function-arg_ ( `,` _function-arg_ )* )? `)` ***right-arrow*** _expression_
>
> _funciton-arg_ ::= ***symbol*** _type-assertion_

The identifiers introduced by _function-arg_ clauses have function scope which comprises the final defining expression. These are bound to parameter values in a function call expression (see _Expressions_ below). The final expression may be of any non-function type.


## Expressions

The expression syntax below is ambiguous in that, for example, a function application or qualified identifier may be parsed as a _boolean-expr_, _algebraic-expr_, or _record-expr_. Each possible parse should be semantically equivalent.

> _expression_ ::= _expression-base_ _type-assertion_? | `(` _expression_ `)`
>
> _expression-base_ ::= _function-literal_ | _value-binding_ | _conditional-expr_ | _boolean-expr_ | _algebraic-expr_ | _record-expr_

Type assertions present another possible ambiguous parse; where ambiguous, type assertions have the highest precedence — see _Precedence_ below. An expression of the form `expr: T` is valid if the type of `expr` is `T` or a supertype of `T`, and ill-formed otherwise. The type of `expr: T`, if well-formed, is always the type `T`.


### Value bindings

> _value-binding_ ::= _let-binding_ | _with-binding_
>
> _let-binding ::= `let` ***symbol*** _type-assertion_? `=` _expression_ `;` _expression_
>
> _with-binding_ ::= `with` _expression_ `;` _expression_

Both `let` and `with` introduce new identifiers in expression context that can mask bindings from outer scopes.

The value of the let expression `let id = expr₁; expr₂` is `expr₂` with `id` bound to `expr₁`.

The value of the where expression `where expr₁; expr₂` is `expr₂` with the bindings `fᵢ = vᵢ` for each field `fᵢ` with value `vᵢ` in the record value of `expr₁`. If `expr₁` does not have record type, the expression is ill-formed.

Example:

```
let a = { scale = 3.2; pos = { x = 3 m ; y = 4 m; }; };   # binds `a` below.
with a.pos;   # binds `x` to 3 m and `y` to 4 m below.
a.scale*(x+y);
```

As the type of a value binding is the type of its final expression, syntactic ambiguity in type assertion parsing has no semantic consequence: `let id = expr₁; expr₂: T` has the type of `expr₂: T`, which is `T` iff the type of `expr₂` is `T` or a supertype of `T` ; similarly, `(let id = expr₁; expr₂): T` has type `T` iff the type of `let id = expr₁; expr₂`, which is the type of `expr₂` is `T` or a supertype of `T`.


### Conditional expressions

> _conditional-expr_ ::= _if-expr_ | _case-expr_
>
> _if-expr_ ::= `if` _boolean-expr_ `then` _expression_ `else` _expression_
>
> _case-expr_ ::= `|` _boolean-expr_ ***right-arrow*** _expression_ )? _case-expr_ | `|` ( `otherwise` | `true` ) ***right-arrow*** _expression_

Alternatives based on one or more conditions can be expressed with if/then/else clauses or case expressions introduced with the bar symbol. In either, the conditional expression is ill-formed if any of the _boolean-expr_ have type which is not boolean.

The value of `if condition then expr₁ else expr₂` is the value of `expr₁` in the case where `condition` evaluates to true, and `expr₂` otherwise. The type of the _if-expr_ is the type of `expr₁` and `expr₂`; the expression is ill-formed if the types of `expr₁` and `expr₂` differ.

The value of `| otherwise → expr` is just the value of `expr`. `true` can be used in place of `otherwise` equivalently. For a compound _case-expr_ of the form `| condition → expr₁ case₂`, the value is `expr₁` in the case where `condition` is true, and the value of `case₂` otherwise; the expression is ill-formed if the types of `expr₁` and `case₂` differ.

Example:

```
# Always evaluates to 10 km.
let a = if 3>2 then 10 m else 2 m; a*1000

# Piece-wise constant reaction rate function.
def rate = fn (T: temperature) →
  | T<0 °C → 10 mM/s
  | T<10 °C → 20 mM/s
  | otherise → 30 mM/s;

```

As with value bindings, there is no semantic ambiguity arising from an ambiguous parse from a trailing type assertion.

### Boolean expressions

> _boolean-expr_ ::= _boolean-term_ ( `or` _boolean-term_ )*
>
> _boolean-term ::= _boolean-factor_ ( `and` _boolean-factor_ )*
>
> _boolean-factor_ ::= `not`? ( _comparison-expr_ | _function_application_ | _qualified-identifier_ | _boolean-literal_ | `(` _boolean-expr_ `)` )
>
> _comparison-expr_ ::= _comparison-base_ _comparison-op_ _comparison-base_
>
> _comparison-op_ ::= ***compare-equal*** | ***compare-not-equal*** | ***compare-less*** | ***compare-less-equal*** | ***compare-greater*** | ***compare-greater-eqaul***
>
> _comparison-base_ ::= _algebraic-expr_ | `(` _expression_ `)`

A compound boolean expression of the form `expr₁ or expr₂` is well defined only if the types of `expr₁` and `expr₂` are both boolean. The value is false if both `expr₁` and `expr₂` have value false, and true otherwise. The `or` operation can be considered to be left associative, but the `or` operation is both commutative and associative, and consequently there is no ambiguity resulting in expression evaluation order.

The boolean operator `and` has higher precedence than that of `or`, and `expr₁ and expr₂` has value true iff `expr₁` has value true and `expr₂` has value true. The expression is ill-formed if `expr₁` or `expr₂` do not have type boolean. Similarly to `or`, the operator can be considered to be left-associative, but there is no ambiguity regardless.

`not` has the highest precedence of the boolean operators, and `not expr` has value true iff `expr` has value false. The expression is ill-formed if `expr` does not have type boolean.

A _comparison-expr_ of the form `expr₁ == expr₂` or `expr₁ ≠ expr₂` is well-definied iff `expr₁` and `expr₂` have the same type, with that type being a quantity type, a record type, or boolean. Its value is true if the two expressions have the same boolean or quantity values or if the expressions are of the same record type and `(expr₁).f == (expr₂).f` for each field `f` in this record type, and is otherwise false. `expr₁ ≠ expr₂`, if well-defined, is true iff `expr₁ == expr₂` is false.

A _comparison-expr_ of the form `expr₁ op expr₂` where `op` is not `==` nor `≠` is well-defined iff `expr₁` and `expr₂` have the same quantity type.  The comparison operators `≥`, `>`, `≤`, `<` evaluate in accordance with the usual order on real values, after applying any requisite scaling to equate units.

When a _comparison-expr_ `expr₁ op expr₂` compares two terms of the same quantity type which may carray an offset value from a non-zero based unit, the value is equivalent to `expr₁ - expr₂ op zero` where `zero` is a zero-valued quantity of the same type as `expr₁`. See _Offset value arithemtic_ below.

A _boolean-expr_ may not involve any boolean or comparison operations, and be equivalently parsed as an algebraic expression. In this instance, its type and value is the same as if it were parsed directly as an algebraic expression.


### Algebraic expressions

> _algebraic-expr_ ::= _algebraic-term_ ( ( ***plus-sign*** | ***minus-sign*** ) _algebraic-term_ )*
>
> _algebraic-term_ ::= _algebraic-factor_ ( ( ***multiplication-dot*** | ***asterisk*** | ***division-slash*** ) _algebraic-factor_ )*
>
> _algebraic-factor ::= _quantity-literal_ | ( ***minus-sign***? ( _function-application_ | _qualified-identifer_ | _record-field-expr_ | ***numeric-literal*** | `(` _expression_ `)` ) _exponent_ )
>
> _exponent_ ::= ***superscript-literal*** | ( `^` _algebraic-factor_ )*

Additive operators `+` and `-` and multiplicative operators `*` (or `·`) and `/` are all left associative. Multiplicative operators have higher precedence than additive operators.

An additive expression of the form `expr₁ + expr₂` or `expr₁ - expr₂` is well defined iff `expr₁` and `expr₂` have the same quantity type. The expresison has the same quantity type, with the value corresponding to the regular arithmetic operation after any requisite scaling to equalize units. For values that may carray an offset from a non-zero based unit, refer to _Offset value arithmetic_ below.

> _function-application_ ::= ( _qualified-identifier_ | `(` _function-literal_ `)` ) `(` ( _expression_ ( `,` _expression_ )* )? `)`

A _function-application_ expression is well-formed iff the function type of the _function-literal_ or the function value bound to _qualified-identifier_ is compatible with the number and types of the _expression_ clauses constituting the arguments. If the function value correspnds to the form `fn (a₁: T₁, …) → result`, then the value of the function application expression with arguments `expr₁`, … is the value of the expression `let a₁: T₁ = expr₁; … result`.

### Record expressions

> _record-expr_ ::= _record-term_ ( ***preferential-union*** _record-term_ )*
>
> _record-term_ ::= _record-literal_ | _function-application_ | _qualified_identifier_ | _record-literal_ | `(` _record-expr_ `)`
>
> _record-field-expr_ ::= ( _record-literal_ | _function-application_ | `(` _record-expr_ `)` ) `.` _qualified-identifier_

Access to a field in a record that is bound to an identifier can be made via the corresponding qualified identifier (see section _Qualified Identifiers_); for records that are not bound to an identifier, the _record-field-expr_ provides the same function, also using the period as an field access operator.

Records of different types can be combined with the ***preferential-union*** operator: given a record _r_ with fields _fᵢ_ and values _vᵢ_ for _i_∈_I_, and another record _s_ with fields _gⱼ_ and values _wⱼ_ for _j_∈_J_, the expression _r_ ⊔ _s_ is a record with a set of fields {_fᵢ_}∪{_gⱼ_}, and the value of a field _h_ in this record is _vᵢ_ if there exists a field _fᵢ_=_h_, else _wⱼ_ for the field _wⱼ_=_h_. Essentially, the record comprises all the fields of the left hand side, together with any fields from the record on the right hand side which don't have the same name as a field in the first record.

### Precedence

**TODO**

### Offset value arithmetic

**TODO** Grab and rewrite comments above re: degrees Celsius.

### Evaluation of expressions

**TODO** Clarify what is meant by this! I.e. difference between mathematical terms and their interpretatoin by the compiler/interpreter.


## Modules and interfaces

Modules are used to collect parameters, constants, and function definitions; interfaces are used to define a particular sort of Arbor functionality, such as ion channel or gap junction dynamics. Module and interface definitions can only be provided at top level.

### Module definition

> _module-defn_ ::= `module` ***symbol*** `{` ( _type-alias_ | _parameter-defn_ | _constant-defn_ | _function-defn_ | _module-import_ )\* `}`
>
> _type-alias_ ::= `type` ***symbol*** `=` _type-expr_ `;`
>
> _parameter-defn_ ::= `parameter` _type-expr_? ***symbol*** `=` _expression_ `;`
>
> _constant-defn_ ::= `def` ***symbol*** _type-assertion_? `=` _expression_ `;`
>
> _constant-defn_ ::= `def` ***symbol*** _type-assertion_? `=` _expression_ `;`
>
> _function-defn_ ::= `fn` ***symbol*** _argument-list_ `=` _expression_ `;`
>
> _argument-list_ ::= `(` ( _type-expr_ ***symbol*** ( `,` _type-expr_ ***symbol*** )* )? `)`
>
> _module-import_ ::= `import` ***symbol*** ( `as` ***symbol*** )
>
> _type-assertion_ ::= `:` _type-expr_

### Interface definition

**TODO**

#### Module imports

Identifiers bound in module scope in one module can be used in another module or interface, if the module is imported. If a module `A` is imported as `B`, a type, parameter, constant, or function _x_ defined in `A` can be referenced with the qualified identifier `B`._x_.

Examples:
```
module A {
    constant real pi = 3;
}

module X {
    import module A;
    constant real pi = A.pi; # Also 3.
}

module Y {
    import module A as B;
    constant real pi = B.pi; # Also 3.
}
```

### Function definitions

**TODO**

### Type aliases

**TODO**

#### Notes

As noted above in the section on identifiers, the same identifier may not be rebound within a module or interface definition, but _is_ permitted to refer to a type in a type context, a value inan expression context, and a module in an module import context.

Type assertions are optional on the left hand side of `def` clauses, as the type of the value bound to the identifier is always deducible from the expression on the right hand side.

The expression on the right hand side of a constant definition may depend only on identifiers bound to constants. Similarly, the expression on the right hand side of a parameter definition may depend only on identifiers bound to constants or other parameters.


### Alternative function and type alias syntax

The forms for function and type alias definitions above are quite different from earlier proposals; these are reprised here if the new proposed forms are rejected:

> _function-defn_ ::= `function` ***symbol*** _argument-list_ ( `->` _type-expr_ )* `{` _expression_ `}`

Type definitions are only for record types:

> _type-alias_ ::= `record` ***symbol*** `{` ( _type-expr_ ***symbol*** `;` )\* `}`

### Parameter semantics

A parameter definition introduces a new identifer in module scope, together with a default value. Parameters can be used in any following expression within the module.
The expression to which a parameter is bound may not include identifiers that are bound in an interface to external quantities, but may include other parameters.

Parameters that are exported in an interface can be bound to a user supplied value externally; they are nonetheless constant. In any expression in the same module scope that uses that parameter, the value of the parameter will be the the user supplied value. This applies to expressions that are bound to other parameters — for example, consider the following module and interface definition.

```
module impl {
    parameter voltage a = 3 mV;
    parameter current I = (13 mV - a) / 20 kΩ;
}

interface point "foo" {
    import impl;
    export parameter impl.a as a;

    effect current = impl.I;
}
```

Models using the "foo" mechanism can set the parameter `a` to some voltage. If it remains unset, the mechanism supplies a non-specific current of 0.5 μA; if the model sets the parameter `a` to -7 mV, the parameter `I` in the `impl` module will have the value 1.0 µA, which will in turn be the supplied non-specific current.

# Refactor from here

**TODO**

Types, records and expressions are described below.

> &lt;interface-defn&gt; ::= `interface` &lt;interface-class&gt; &lt;string-literal&gt; `{` [ &lt;pararameter-defn&gt; | &lt;export&gt; | |&lt;combined-export-parameter-defn&gt; | &lt;constant-defn&gt; | &lt;record-alias-defn&gt; | &lt;function-defn&gt; | &lt;module-import&gt; | &lt;binding&gt; | &lt;initial-decl&gt; | &lt;regime-defn&gt; | &lt;regime-internal-decl&gt;]* `}`
>
> &lt;export&gt; ::= `export` [`density`] `parameter` [&lt;type-expr&gt;] &lt;qualified-identifier&gt; [ `as` &lt;identifier&gt; ]
>
> &lt;combined-export-parameter-defn&gt; ::= `export` [`density`] <parameter-defn>
>
> &lt;binding&gt; ::= `bind` [&lt;type-expr&gt;] &lt;identifer&gt; = &lt;bindable-state&gt; `;`
>
> &lt;initial-defn&gt; ::= `initial` [`regime` &lt;qualified-identifier&gt;] [ &lt;initial-post-expr&gt; `from` ] [&lt;type-expr&gt;] `state` `=` &lt;expression&gt; `;`
>
> &lt;initial-post-expr&gt; ::= `steady` | `evolve` `for` &lt;expression&gt;
>
> &lt;regime-defn&gt; ::= `regime` &lt;identifier&gt; `{` [ &lt;regime-internal-defn&gt; | &lt;regime-defn&gt; ] `}`
>
> &lt;regime-internal-defn&gt; ::= `evolve-defn` | &lt;when-defn&gt; | &lt;effect-defn&gt;
>
> &lt;evolve-defn&gt; ::= `evolve` [`explicit`] [&lt;type-expr&gt;] `state'` `=` &lt;expression&gt; `;`
>
> &lt;when-defn&gt; ::= `when` &lt;when-condition&gt; `;` [ `regime` &lt;qualified-identifier&gt; ] `state` `=` &lt;expression&gt; `;`
>
> &lt;when-condition&gt; ::= &lt;expression&gt; | [&lt;type-expr&gt;] &lt;identifier&gt; = ( `event` | `post` )
>
> &lt;effect-defn&gt; ::= &lt;effect&gt; = &lt;expression&gt; `;`

The sets of possible bindable states, effects, and interface classes are finite, but may be extended in the future. Not all bindable states or effects are permissible in all interface classes. `density` parameters are intended to describe parameters that may be linearly interpolated, and are only supported in interface classes where this is meaningful.

> &lt;interface-class&gt; ::= `density` | `discrete` | `concentration`
>
> &lt;bindable-state&gt; ::= `state` | `membrane` `potential` | `temperature` | (`current` `density` | `molar` `flux`) &lt;species-name&gt;) | (`internal` | `external`) `concentration` &lt;species-name&gt; | `charge` &lt;species-name&gt;
>
> &lt;effect&gt; ::= `current` `density` [&lt;species-name&gt;] | `molar` `flow` `rate` &lt;species-name&gt; | `current` [&lt;species-name&gt;] | `molar` `flux` &lt;species-name&gt; | (`internal` | `external`) `concentration`
>
> &lt;species-name&gt; ::= &lt;string-literal&gt;

## Module semantics

**TODO:** explain parameters; module imports.

## Interface semantics

An interface provides, generally:

1. A _state_ that evolves over time, as a function of various local cellular quantities.
2. A set of user-settable parameters.
3. A set of _effects_ that the state imposes locally upon the cell.

The initial state and its type is given by the `initial` declaration. For example,
```
    initial state = { a = 0 S; b = 0 S; };
```
declares the state has type `record { conductance a; conductance b; }` and that its initial
value has zero in both fields.

The initial state can be qualified with `steady` or `evolve for` to indicate that the provided value should be integrated over time by some given value or until steady state before being used as an initial value. The evolution is that given by subsequent `evolve` declarations. The degree to which this is supported in an implementation will depend very much on the nature of the evolution function(s).

Evolution is governed by one or more _regimes_. In the simplest case, there is just the one, default regime, but more generally, multiple regimes may be defined, and transitions between them can be triggered by boolean-valued expressions that are a function of state and bound cell quantities, or by pre- or post-synaptic events. Regime definitions can be nested, and an inner regime definition inherits any triggers from the enclosing scope, and the evolution function from the enclosing scope as well, if it is not overridden. The initial regime can be specified in the `initial` definition. The default regime has no name, and so cannot be a target of `regime` clauses in a `when` definition.

The evolution function is defined with the `evolve` definition, and there may only be one `evolve` per regime. The expression on the rhs must have type equal to the 'derivative' type of the state. The derivative of a quantity is that quantity divided by time; the derivative of a record type is the record type where each field _x_ is replaced by a field _x'_ with type the derivative type of _x_. See the discusion under record types below for more details.

Effects tie the state to the cell, defining how the state determines ionic currents or species flows, or how it governs species concentrations.

Exports make parameters visible and settable by models that employ the interface. As a convenience, an export and a parameter definition can be combined in a single export declaration.

If the interface doesn't require any evolving state, it can omit the `initial` and `evolve` definitions.

### Density models

Density models represent a process which is distributed over an areal extent of the cell membrane. Parameters defined in density models may be labelled as `density` parameters — such parameters are declared to be safe to interpolate, and can be used with spatitally varying scaling, for example.

Bindable states: state; membrane potential; temperature; internal/external concentration "_species_"; charge "_species_".

Effects: current density (non-specific); current density "_species_"; molar flux "_species_".

Events: only boolean-valued expressions.

### Discrete models

Discrete models capture localized effects and can react in response to incoming spike events or post-synaptic spike detection events. They may not have `density` parameters.

Bindable states: state; membrane potential; temperature; internal/external concentration "_species_"; charge "_species_".

Effects: current (non-specific); current "_species_"; molar flow rate "_species_".

Events: any.

### Concentration models

Concentration models determine the internal and/or external concentration of a species over time. As specified, only one concentration model can be used to determine the state of a species in any given region of a cell. The initial value of the state determines the initial value(s) of the concentration.

Bindable states: state; membrane potential; temperature; current density "_species_"; molar flux "_species_"; charge "_species_". Note that the concentrations themselves are _not_ bindable.

Effects: internal/external concentration "_species_".

Events: only boolean-valued expressions.

### Alternative concentration model

There are a couple of problems with specifying concentration evolution with concentration models as outlined above:
* Concentration models cannot be combined for any given species.
* Initial concentration is determined by the model, disregarding any user-supplied initial concentration data.

A cleaner approach would be to present a concentration model in terms of the change in concentration over time. Then the initial value would be user-specified, and multiple concentration model contributions could overlap.

Example, concentration model:
```
interface concentration "CaDynamics" {
    export parameter real gamma = 0.5;
    export parameter time decay = 80 ms;
    export parameter length detph = 0.1 µm;
    export parameter concentration steady_conc = 1.0e-4 mmol/L;

    initial state = steady_conc;

    bind ca_conc = state;
    bind ca_flux = molar flux "ca";
    evolve state' = -ca_flux*gamma/depth - (ca_conc-steady_conc)/decay;

    effect internal concentration "ca" = ca_conc;
}
```

Alternative concentration rate model:
```
interface concentration "CaDynamics" {
    export parameter real gamma = 0.5;
    export parameter time decay = 80 ms;
    export parameter length detph = 0.1 µm;
    export parameter concentration steady_conc = 1.0e-4 mmol/L;

    bind ca_flux = molar flux "ca";
    bind ca_conc = internal concentration "ca";
    effect internal concentration rate "ca" =
            -ca_flux*gamma/depth - (ca_conc-steady_conc)/decay;
}
```

The alternative model could have been split into two different models, to be applied over the same regions:
```
interface concentration "CaUnbuffered" {
    export parameter real gamma = 0.5;
    export parameter length depth = 0.1 µm;

    bind ca_flux = molar flux "ca";
    effect internal concentration rate "ca" = -ca_flux*gamma/depth;
}

interface concentration "CaBuffered" {
    export parameter concentration steady_conc = 1.0e-4 mmol/L;
    export parameter time decay = 80 ms;

    bind ca_conc = internal concentration "ca";
    effect internal concentration rate "ca" = -(ca_conc-steady_conc)/decay;
}
```

## Magic keywords and extension points

Within an interface block, there are specific points within the permitted syntax where keywords are used to refer to interface-specific values or concepts, and which constitute natural places for future extensions of the interface block to support new functionality:

1. Right hand side of `bind`, corresponding to interface-specific exposed values, such as the mechanism state, ion concentrations, membrane voltage, etc.
2. Right hand side of local binding in a `when` clause, tying event or post-event data to an identifier.
3. Left hand side of `effect`, corresponding to contributions of the interface to the simulation state.
4. Left hand side of `initial`, where the optional `... from` clause modifies the initial state, and also where `state` forms the left hand side of the final binding.
5. Left hand side of `evolve`, where the keyword `explicit` might be replaced with other possible descriptions, such as `implicit` for implicit ODE or DAE systems, and also where `state'` forms left hand side of the final binding.
6. Left hand side of `export`, where we have `density parameter` or `parameter`, but which could be extended to support for example the exposure of derived values to probe requests or similar.

**TODO:** Add examples of possible syntax extensions in these cases.
